plugins {
    id 'java'

    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'

    id 'org.openapi.generator' version '7.7.0'
}

group = 'com.akisha'
version = '0.0.1'
description = 'Basic E-Commerce project'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sourceSets {
    main {
        java {
            srcDir layout.buildDirectory.dir("generated/src/main/java")
        }
    }
    integrationTest {
        java {
            srcDir file("src/integrationTest/java")
        }
        resources {
            srcDir file("src/integrationTest/resources")
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.projectlombok:lombok'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mongodb'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()

    reports {
        html.required.set(true)
        junitXml.required.set(true)
    }

    shouldRunAfter test
}

check.dependsOn integrationTest

openApiGenerate {
    generatorName = "spring"
    inputSpec = "${projectDir}/src/main/resources/openapi.yaml".toString()
    outputDir = layout.buildDirectory.dir("generated").get().asFile.path
    apiPackage = "com.akisha.spring_ecommerce_backend.controller"
    modelPackage = "com.akisha.spring_ecommerce_backend.dto"
    configOptions = [
            interfaceOnly                 : "true",
            useTags                       : "true",
            library                       : "spring-boot",
            useJakartaEe                  : "true",
            lombok                        : "true",
            additionalModelTypeAnnotations: """
            @lombok.Builder
            @lombok.NoArgsConstructor
            @lombok.AllArgsConstructor
        """
    ]
}